(use 'std/glass.ae')

(let quad-offset 60.0)
(let quad-width 25.0)
(let quad-height 120.0)
(let quad-speed 1.5)
(let circle-radius 10.0)
(let circle-diameter (* circle-radius 2.0))
(let circle-speed 2.5)

(let boxes-collide [a-pos-x a-pos-y a-width a-height
                    b-pos-x b-pos-y b-width b-height] ->
  (and (and (>= (+ a-pos-x a-width) b-pos-x)
            (<= a-pos-x (+ b-pos-x b-width)))
       (and (>= (+ a-pos-y a-height) b-pos-y)
            (<= a-pos-y (+ b-pos-y b-height)))))

(let init [] -> {
  'left-move': 0.0
  'right-move': 0.0
  'left-pos-x': quad-offset
  'left-pos-y': 0.0
  'right-pos-x': 0.0
  'right-pos-y': 0.0
  'circle-move-x': 1.0
  'circle-move-y': -1.0
  'circle-pos-x': 0.0
  'circle-pos-y': 0.0
})

(let event-handler [state event] ->
  (if (== event::'type' 'key-press')
    (match event::'key-code'
      'w' -> (set state::'left-move' -1.0)
      's' -> (set state::'left-move' 1.0)
      'up' -> (set state::'right-move' -1.0)
      'down' -> (set state::'right-move' 1.0))
   elif (== event::'type' 'key-release')
    (match event::'key-code'
      'w' -> (if (== state::'left-move' -1.0) (set state::'left-move' 0.0))
      's' -> (if (== state::'left-move' 1.0) (set state::'left-move' 0.0))
      'up' -> (if (== state::'right-move' -1.0) (set state::'right-move' 0.0))
      'down' -> (if (== state::'right-move' 1.0) (set state::'right-move' 0.0)))
   elif (== event::'type' 'resize')
    (set state::'right-pos-x' (- (- event::'width' quad-width)
                                 quad-offset))))

(let update [state] ->
  (let size (glass/window-size))

  (set state::'left-pos-y' (+ state::'left-pos-y'
                              (* state::'left-move' quad-speed)))
  (if (< state::'left-pos-y' 0.0)
    (set state::'left-pos-y' 0.0)
   elif (> (+ state::'left-pos-y' quad-height) size::'height')
    (set state::'left-pos-y' (- size::'height' quad-height)))

  (set state::'right-pos-y' (+ state::'right-pos-y'
                               (* state::'right-move' quad-speed)))
  (if (< state::'right-pos-y' 0.0)
    (set state::'right-pos-y' 0.0)
   elif (> (+ state::'right-pos-y' quad-height) size::'height')
    (set state::'right-pos-y' (- size::'height' quad-height)))

  (set state::'circle-pos-x' (+ state::'circle-pos-x'
                                (* state::'circle-move-x' circle-speed)))
  (if (< state::'circle-pos-x' circle-radius)
    (set state::'circle-move-x' 1.0)
   elif (> (+ state::'circle-pos-x' circle-radius) size::'width')
    (set state::'circle-move-x' -1.0))

  (set state::'circle-pos-y' (+ state::'circle-pos-y'
                                (* state::'circle-move-y' circle-speed)))
  (if (< state::'circle-pos-y' circle-radius)
    (set state::'circle-move-y' 1.0)
   elif (> (+ state::'circle-pos-y' circle-radius) size::'height')
    (set state::'circle-move-y' -1.0))

  (if (boxes-collide (- state::'circle-pos-x' circle-radius)
                     (- state::'circle-pos-y' circle-radius)
                     circle-diameter circle-diameter
                     state::'left-pos-x'
                     state::'left-pos-y'
                     quad-width quad-height)
    (if (<= state::'circle-pos-x'
            state::'left-pos-x')
      (set state::'circle-move-x' -1.0)
     else
      (set state::'circle-move-x' 1.0)))

  (if (boxes-collide (- state::'circle-pos-x' circle-radius)
                     (- state::'circle-pos-y' circle-radius)
                     circle-diameter circle-diameter
                     state::'right-pos-x'
                     state::'right-pos-y'
                     quad-width quad-height)
    (if (<= state::'circle-pos-x'
            state::'right-pos-x')
      (set state::'circle-move-x' -1.0)
     else
      (set state::'circle-move-x' 1.0))))

(let render [state] ->
  (glass/clear 0.0 0.0 0.0 1.0)
  (glass/quad state::'left-pos-x' state::'left-pos-y'
              quad-width quad-height 1.0 1.0 1.0 1.0)
  (glass/quad state::'right-pos-x' state::'right-pos-y'
              quad-width quad-height 1.0 1.0 1.0 1.0)
  (glass/circle state::'circle-pos-x' state::'circle-pos-y'
                circle-radius 1.0 1.0 1.0 1.0))

(glass/run 'Hello, OpenGL!' 640 480 init event-handler update render)
